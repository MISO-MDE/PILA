grammar co.edu.uniandes.Pagos with org.eclipse.xtext.common.Terminals
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

//Gramatica conforme al siguiente metamodelo
//import "platform:/resource/co.edu.uniandes.gramaticaPayments/metamodel/Pagos.ecore"
//import "http://www.edu.co/uniandes/Pagos"

//Si se desea generar el metamodelo a partir de la gramatica descomentarear
generate pagos "http://www.edu.co/uniandes/Pagos"


Application:
	LBRACE
		'name' COLON name = ID
		COMMA
		'intermediary' COLON intermediary = Intermediary
	RBRACE
	;
	
Intermediary:
	{Intermediary}
	LBRACE
		'name' COLON name = ID
		COMMA
		'superentities' COLON LBRACKET superentitie += SuperEntity (COMMA superentitie += SuperEntity)* RBRACKET
	RBRACE
	;

SuperEntity:
	{SuperEntity}
	LBRACE
		'name' COLON name = ID
		COMMA
		'description' COLON description = STRING
		COMMA
		'attributes' COLON 
			LBRACKET attLists += Attribute (COMMA attLists += Attribute)* RBRACKET
		COMMA
		'entities' COLON LBRACKET participante += Participant (COMMA participante += Participant)* RBRACKET		
	RBRACE
;

Participant:
	{Participant}
	LBRACE
		'name' COLON name = ID
		COMMA
		'description' COLON description = STRING
		COMMA
		'attributes' COLON 
			LBRACKET attLists += Attribute (COMMA attLists += Attribute)* RBRACKET
	RBRACE
	(COMMA
	'InformationTemplate' COLON LBRACKET templatesForm += InformationTemplate (COMMA templatesForm += InformationTemplate)*)? RBRACKET
	;

Provider:
	{Provider}
	LBRACE
		'name' COLON name = ID
		COMMA
		'description' COLON description = STRING
		COMMA
		'attributes' COLON 
			LBRACKET attLists += Attribute (COMMA attLists += Attribute)* RBRACKET
	RBRACE
	;

InformationTemplate:
	{InformationTemplate}
	LBRACE
		'name' COLON name = ID
		COMMA
		'description' COLON description = STRING
		COMMA
		'FormAttributes' COLON 
			LBRACKET formLists += Attribute (COMMA formLists += Attribute)* RBRACKET
	RBRACE
	(COMMA
	'calculationtemplates' COLON LBRACKET templates += CalculationTemplate (COMMA templates += CalculationTemplate)* RBRACKET)?	
	(COMMA 'providers' COLON LBRACKET providers += Provider (COMMA providers += Provider)* RBRACKET)?
;

CalculationTemplate:
	{CalculationTemplate}
	LBRACE
		'name' COLON name = ID
		COMMA
		'description' COLON description = STRING
		COMMA
		'calculationattributes' COLON 
			LBRACKET attLists += Attribute (COMMA attLists += Attribute)* RBRACKET
		COMMA
		'calculationrules' COLON formula += Formula
	RBRACE
	;

Formula returns Formula:
	{Formula}
	formulaBody += (IfBlock)* | formulaReturn = ReturnBlock;

IfBlock returns IfBlock:
	{IfBlock}
	ifSentence = IfCondition (ELSE elseSentence = ElseSegment)? 
;

IfCondition returns IfCondition:
	{IfCondition}
		IF LBRACKET
			logExp += ExpresionLogica
			RBRACKET
		THEN
		LBRACE
			RETURN COLON expression += Adicion
		RBRACE
	;

ElseSegment returns ElseSegment:
	{ElseSegment}
		LBRACE ifSentence = Formula RBRACE

;

ReturnBlock returns ReturnBlock:
	{ReturnBlock}
	RETURN COLON expression += Adicion
;


ExpresionLogica returns ExpresionLogica:
	variable += TerminalValue (logOper += OPERADORLOGICO (variable2 += TerminalValue | literal += STRING))+
			(LOGICALCONN exp += ExpresionLogica)?
;


Adicion returns Expression:
	Multiplicacion ({Adicion.left = current} operator = mathADD rexp = Multiplicacion)*
;

Multiplicacion returns Expression:
		SigleExp ({Multiplicacion.left = current} operator = mathMULT rexp = SigleExp)*
		;

SigleExp returns Expression:
	TerminalValue | '(' Adicion ')'
;

Attribute:
	{Attribute}
	LBRACE
		name = ID COLON type = TYPE (':=' exp = Adicion)?
	RBRACE 
	;
	
TerminalValue returns TerminalValue:
	{TerminalValue}
	variable = [Attribute] | valor = Number 
;

mathADD returns mathADD:
	{mathADD}
	operador = ADDOP
;

mathMULT returns mathMULT:
	{mathMULT}
	operador = MULTOP
;


Number:
	EDouble | Integer
;

EDouble returns ecore::EDouble:
	INT? '.' INT (('E'|'e') INT)?;

Integer returns ecore::EInt:
	INT
;

EString returns ecore::EString:
	STRING | ID;
	
/* Terminals */

terminal IF:'SI';
terminal THEN:'ENTONCES';
terminal ELSE: 'SINO';
terminal RETURN: 'RETORNAR';

terminal TYPE: 'String' | 'Integer' | 'Date' | 'Double';

terminal MULTOP: '*' | '/';
terminal ADDOP: '+' | '-';

terminal LOGICALCONN: '&&' | '||';

terminal ID:('a'..'z'|'A'..'Z'|'_')('a'..'z'|'A'..'Z'|'_'|'0'..'9')*(('.')('Ultima' | 'Primera' | 'Tamano'))*;
terminal OPERADORLOGICO: ('<' | '>' | '<=' | '>=' | '=' | '<>');

terminal LBRACE: "{";
terminal RBRACE: "}";
terminal LBRACKET:"[";
terminal RBRACKET: "]";
terminal COMMA: ",";
terminal COLON: ':';
terminal DASH: "#";

terminal STRING:
	'"' (STR_ESC | !('"'|'\r'|'\n'|'\\'))* '"'
	;
	
terminal STR_ESC:
	'\\'('b'|'t'|'n'|'f'|'r'|'"'|'\\'|'$')
	;